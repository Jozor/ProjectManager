function populateProjectsList()
{
   updateBreadcrumbList("ProjectsPage");
   
   MainViewStack.hidden = false;
   MainViewStack.clear();
   
   MainMenuGui.setActionBarVisible(true);
   
   ActionButtonPanelList.clear();
   
   %projectCount = getProjectCount();
   for(%i=1; %i <= %projectCount; %i++)
   {
      %project = getProject(%i);
      %projectName = %project.projectName;
      %localPath = %project.localPath;
      
      if(%projectName !$= "")
      {
         if(MainMenuGui.searchText !$= "")
         {
            if(!strIsMatchExpr("*" @ MainMenuGui.searchText @ "*", %projectName))
               continue;
         }
         
         %tags = getEngineBuildTags(%project.engineBuildId);
         
         %localPath = IsDirectory(%project.localPath) ? %project.localPath : %project.localPath SPC "(Not Found)";
         %card = createCard(%project.projectName, %localPath, %tags, "", true, "navToProjectProps("@%project@");", "PreviewImage", "", %project.previewImage, "");
         
         MainViewStack.add(%card);
      }
   }
   
   //Now add our action panel buttons
   ActionButtonPanelList.clear();
   
   %actionBtn = new GuiIconButtonCtrl()
   {
      position = "0 0";
      extent = "100 45";
      profile = GuiMenuButtonProfile;
      buttonMargin = "0 -10";
      bitmapAsset = "ManagerUI:plus_image";
      iconLocation = "Center";
      sizeIconToButton = true;
      makeIconSquare = true;
      textLocation = "Bottom";
      text = "Add New";
      command = "$CurrentEngineBuild = \"\";CreateNewProject();";
   };
   
   ActionButtonPanelList.add(%actionBtn);
}

function navToProjectProps(%project)
{
   MainMenuGui.currentPage = "ProjectPropertiesPage";
   $CurrentProject = %project;

   populateProjectProperties(); 
   
   navToAnimation();
}