function populateEngineBuildsList()
{
   $creatingNew = false;
   
   updateBreadcrumbList("EngineBuildPage");
   
   MainMenuGui.setActionBarVisible(true);
   
   MainMenuSearchBar.hidden = false;
   MainMenuSearchClearBtn.hidden = false;
   
   MainViewStack.hidden = false;
   MainViewStack.clear();
   
   %dlRoll = new GuiRolloutCtrl() { 
      caption = "Downloaded";
      extent = MainViewStack.extent.x SPC 40;
      profile = GuiMenuRolloutProfile;
      margin = "5 5 5 5";
      
      new GuiStackControl() {
         internalName = "stack";
         padding = "5";
      };
   };
   
   MainViewStack.add(%dlRoll);
   
   if(!$TorqueManager::gitNotFound)
   {
      %availRoll = new GuiRolloutCtrl() { 
         caption = "Available For Download";
         extent = MainViewStack.extent.x SPC 40;
         profile = GuiMenuRolloutProfile;
         margin = "5 5 5 5";
         
         new GuiStackControl() {
            internalName = "stack";
            padding = "5";
         };
      };
      
      MainViewStack.add(%availRoll);
   }
   
   %buildCount = getEngineBuildCount();
   for(%i=1; %i <= %buildCount; %i++)
   {
      %bld = getEngineBuild(%i);
      
      //%bld.dump();
      
      if(%bld.buildName !$= "")
      {
         %tags = getEngineBuildTags(%i);
         
         if(MainMenuGui.searchText !$= "")
         {
            if(!strIsMatchExpr("*" @ MainMenuGui.searchText @ "*", %bld.buildName) &&
               !isSearchTextInTags(%tags, MainMenuGui.searchText))
               continue;
         }
         
         %localPath = IsDirectory(%bld.localPath) ? %bld.localPath : %bld.localPath SPC "(Not Found)";
         %ctrl = createCard(%bld.buildName, %localPath, %tags, "", true, "navToEngineBuildProps("@%bld@");");
         
         if(%bld.localPath !$= "" && IsDirectory(%bld.localPath))
         {
            %dlRoll-->stack.add(%ctrl);
         }
         else if(%bld.localPath !$= "" && !IsDirectory(%bld.localPath))
         {
            //if we have a localPath defined but it's not a valid dir, we'll just indicate it's 
            //(Not Found) to let the user resolve the discrepency
            %dlRoll-->stack.add(%ctrl);
         }
         else
         {
            if(%bld.gitPath !$= "")
            {
               %availRoll-->stack.add(%ctrl);
            }
         }
      }
   }
   
   //Now add our action panel buttons
   ActionButtonPanelList.clear();
   
   AddActionButton("ManagerUI:cloud_add_image", "Add From Web", "CreateNewEngineBuild(\"Git Repository\");", "");
   AddActionButton("ManagerUI:zoom_image", "Locate Existing", "CreateNewEngineBuild(\"Local Path\");", "");
   
   MainPanelFooter.clear(); //clear the buttons if they're active
}

function navToEngineBuildProps(%bld)
{
   MainMenuGui.currentPage = "EngineBuildPropertiesPage";
   $CurrentEngineBuild = %bld;

   populateEngineBuildProperties(); 
   
   navToAnimation();
}

function isEngineBuildDownloaded(%build)
{
   if(%build.LocalPath $= "")
      return false;
   
   if(%build.isBinary)
   {
      %fullLocalPath = makeFullPath(%build.LocalPath);
      %mainScript = %fullLocalPath @ "/main." @ $TorqueScriptFileExtension; 
      if(isFile(%mainScript))
         return true;
   }
   else
   {
      %fullLocalPath = makeFullPath(%build.LocalPath);
      if(isDirectory(%fullLocalPath @ "/engine/source/"))
         return true;
   }
   
   return false;
}