function populateProjectsList()
{
   updateBreadcrumbList("ProjectsPage");
   
   MainViewStack.hidden = false;
   MainViewStack.clear();
   
   MainMenuGui.setActionBarVisible(true);
   
   MainMenuGui.mode = "Projects";
   
   ActionButtonPanelList.clear();
   
   %projectCount = getProjectCount();
   for(%i=0; %i < %projectCount; %i++)
   {
      %project = getProject(%i);
      %projectName = %project.projectName;
      %localPath = %project.localPath;
      
      if(%projectName !$= "")
      {
         if(MainMenuGui.searchText !$= "")
         {
            if(!strIsMatchExpr("*" @ MainMenuGui.searchText @ "*", %projectName))
               continue;
         }
         
         %tags = getEngineBuildTags(%project.engineBuildId);
         
         %card = createCard(%project.projectName, %project.localPath, %tags, "", true, "", "PreviewImage", "", %project.previewImage, "");
         
         MainViewStack.add(%card);
      }
   }
   
   //Now add our action panel buttons
   ActionButtonPanelList.clear();
   
   %actionBtn = new GuiIconButtonCtrl()
   {
      position = "0 0";
      extent = "100 45";
      profile = GuiMenuButtonProfile;
      buttonMargin = "0 -10";
      bitmapAsset = "ManagerUI:plus_image";
      iconLocation = "Center";
      sizeIconToButton = true;
      makeIconSquare = true;
      textLocation = "Bottom";
      text = "Add New";
      command = "startCreateNewProjectWizard();";
   };
   
   ActionButtonPanelList.add(%actionBtn);
}