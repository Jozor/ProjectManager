function populateProjectsList()
{
   $creatingNew = false;
   
   updateBreadcrumbList("ProjectsPage");
   
   MainViewStack.hidden = false;
   MainViewStack.clear();
   
   MainMenuGui.setActionBarVisible(true);
   
   ActionButtonPanelList.clear();
   
   %projectCount = getProjectCount();
   for(%i=1; %i <= %projectCount; %i++)
   {
      %project = getProject(%i);
      %projectName = %project.projectName;
      %localPath = %project.localPath;
      
      if(%projectName !$= "")
      {
         if(MainMenuGui.searchText !$= "")
         {
            if(!strIsMatchExpr("*" @ MainMenuGui.searchText @ "*", %projectName))
               continue;
         }
         
         %tags = getEngineBuildTags(%project.engineBuildId);
         
         %localPath = IsDirectory(%project.localPath) ? %project.localPath : %project.localPath SPC "(Not Found)";
         %card = createCard(%project.projectName, %localPath, %tags, "", true, "navToProjectProps("@%project@");", "", "", %project.previewImage, "");
         
         MainViewStack.add(%card);
      }
   }
   
   //Now add our action panel buttons
   ActionButtonPanelList.clear();

   AddActionButton("ManagerUI:plus_image", "Create New", "CreateNewProject(\"New\");", "");
   AddActionButton("ManagerUI:zoom_image", "Locate Existing", "CreateNewProject(\"Existing\");", "");
   
   MainPanelFooter.clear(); //clear the buttons if they're active
}

function navToProjectProps(%project)
{
   MainMenuGui.currentPage = "ProjectPropertiesPage";
   $CurrentProject = %project;

   populateProjectProperties(); 
   
   navToAnimation();
}