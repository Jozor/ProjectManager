function MainMenuGui::deleteFolder(%this, %folderPath)
{
   %fullPath = makeFullPath(%folderPath);
   
   return deleteDirectory(%fullPath);
}

function prepFolderCopy(%fromFolder, %toFolder, %finishedCallback)
{
   if(!isObject($FileCopyList))
      $FileCopyList = new ArrayObject();
      
   $FileCopyList.empty();
   $FileCopyList.fromFolder = %fromFolder;
   $FileCopyList.toFolder = %toFolder;
   $FileCopyList.finishedCallback = %finishedCallback;
}

function copyProjectFolder(%fromFolder, %toFolder, %finishedCallback)
{
   prepFolderCopy(%fromFolder, %toFolder, %finishedCallback);
   
   processCopyFiles(%fromFolder, %toFolder);
   
   runFileCopy();
}

function processCopyFiles(%fromFolder, %toFolder)
{
   %file = findFirstFileMultiExpr( %fromFolder @ "/*.*", false);

   while( %file !$= "" )
   {    
      %isGitPath = (strIsMatchExpr("*.git", %file) || strIsMatchExpr("*.git/", %file));
      //%isGitPath = strIsMatchExpr("*.git/*", %file);
      if(!%isGitPath)
      {
         %destFile = strreplace(%file, %fromFolder, %toFolder);
         
         $FileCopyList.add(%file, %destFile);
      }

      %file = findNextFileMultiExpr( %fromFolder @ "/*.*" );
   }
   
   //do sub directories
   %paths = getDirectoryList(%fromFolder);
   for(%i=0; %i < getFieldCount(%paths); %i++)
   {
      %childPath = getField(%paths, %i);
      
      if(%childPath $= ".git")
         continue;
         
      processCopyFiles(%fromFolder @ %childPath @ "/", %toFolder @ %childPath @ "/");
   }
   
   $FileCopyList.totalFileCount = $FileCopyList.count();
   $FileCopyList.totalProcessedCount = 0;
}

function runFileCopy()
{
   %pageCount = 200;
   %currentIndex = 0;
   while($FileCopyList.count() != 0)
   {
      %fromFile = $FileCopyList.getKey(0);
      %destFile = $FileCopyList.getValue(0);
      
      %destPath = filePath(%destFile) @ "/";
      if(!isDirectory(%destPath))
         createPath(%destPath);
   
      %success = pathCopy(%fromFile, %destFile, false);
      if(!%success)
      {
         $FileCopyList.hadErrors = true;
         error("copyProjectFolder() - failed to copy file: " @ %fromFile);
      }
      
      $FileCopyList.erase(0);
      %currentIndex++;
      $FileCopyList.totalProcessedCount++;
      
      %copyProgressPercent = $FileCopyList.totalProcessedCount/$FileCopyList.totalFileCount;
      
      ProgressBarDlg.update("Copying Files", %copyProgressPercent);
      
      if(%currentIndex >= %pageCount)
      {
         schedule(100, 0, runFileCopy);
         Canvas.repaint();
         %currentIndex = 0;
         break;  
      }
   }
   
   if($FileCopyList.count() == 0)
   {
      eval($FileCopyList.finishedCallback);
   }
}